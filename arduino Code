#include <OneWire.h>
#include <DallasTemperature.h>
#include <PubSubClient.h>
#include <WiFi.h>
#include "DHT.h"


// network settings
const char* WIFI_SSID = "Your WiFi SSID"; // Your WiFi SSID
const char* WIFI_PASSWORD = "Your WiFi password"; // Your WiFi password
const char* MQTT_SERVER = "Your VM instance public IP address"; // Your VM instance public IP address
const char* MQTT_TOPIC = "iot"; // MQTT topic for subscription
const int MQTT_PORT = 1883; // Non-TLS communication port

unsigned long lastMQTTSend = 0; 
const unsigned long mqttInterval = 4500; 

char buffer[256] = ""; // Text buffer

WiFiClient espClient;
PubSubClient client(espClient);

// pin assignment
const int oneWireBus = 9; // pin A1, soil tmeperature sensor
const int moisturePin = 6; // makerport, soil moisture sensor
const int irPin = 47; // IR Distance sensor
const int dht11Pin = 21; // DHT11 sensor pin

#define DHTTYPE DHT11
DHT dht(dht11Pin, DHTTYPE);

const int relayPin = 5; // relaypin

// setup a oneWire instance to communicate with OneWire devices
OneWire oneWire(oneWireBus);

// pass oneWire reference to soil temperature sensor 
DallasTemperature sensors(&oneWire);

// define soil moisture sensor configurations
int MinDepth = 4095;
int MaxDepth = 2170; 

// system startup configuration
unsigned long systemStartTime;
const unsigned long startUpDelay = 15000;

// pump
bool pumpState = false;
const unsigned long pumpDuration = 5000; // pump runs for 5 sec
unsigned long pumpStartTime = 0;

void reconnect() {
 while (!client.connected())
 {
 Serial.println("Attempting MQTT connection...");
 if(client.connect("ESP32Client")) {
 Serial.println("Connected to MQTT server");
 }
 else {
 Serial.print("Failed, rc=");
 Serial.print(client.state());
 Serial.println(" Retrying in 5 seconds...");
 delay(5000);
 }
 }
}

void setup_wifi() {
 delay(10);
 Serial.println();
 Serial.print("Connecting to ");
 Serial.println(WIFI_SSID);
 WiFi.mode(WIFI_STA);
 WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
 while (WiFi.status() != WL_CONNECTED) {
 delay(500);
 Serial.print(".");
 }
 Serial.println("");
 Serial.println("WiFi connected");
 Serial.print("IP address: ");
 Serial.println(WiFi.localIP());
}

void setup() {
  // start the serial monitor
  Serial.begin(115200);

  pinMode(relayPin, OUTPUT);

  // relay off (Active LOW)
  digitalWrite(relayPin, LOW);
  delay(300);

  // start sensor
  sensors.begin();
  dht.begin();

  pinMode(irPin, INPUT_PULLUP);

  systemStartTime = millis();

  // Connect to the WiFi network
  setup_wifi(); 

  // Set up the MQTT client
 client.setServer(MQTT_SERVER, MQTT_PORT); 
}

void loop() {
  if(!client.connected()) {
    reconnect();
  }
  client.loop();
  

  // read soil temperature sensor value
  sensors.requestTemperatures(); 
  float temperatureC = sensors.getTempCByIndex(0);

  // read soil moisture sensor value
  int sensorValue = analogRead(moisturePin);
  int outputValue = map(sensorValue, MaxDepth, MinDepth, 100, 0);
  int percent = constrain(outputValue, 0, 100);

  // read ambient temperature
  float temperature = dht.readTemperature();

  // read ir value
  bool irDetected = digitalRead(irPin);
  
  if(millis() - systemStartTime > startUpDelay){
    
    // pump initiation - runs for 5 seconds if true
    if(percent <= 60 && !pumpState){
      Serial.println(pumpState);
      digitalWrite(relayPin, HIGH); //pump starts
     pumpState = true;
     pumpStartTime = millis();

   } 
  }
  
  
  if (pumpState && (millis() - pumpStartTime >= pumpDuration)) {
  // pump stops after 5 seconds
  digitalWrite(relayPin, LOW);  // off
  pumpState = false;
}

   //output in serial monitor
  Serial.print(temperatureC);
  Serial.println("ÂºC");
  Serial.print(String(percent) + " %\t");
  Serial.println(pumpState? "ON": "OFF");
  

  // send to MQTT, JSON payload
  if (millis() - lastMQTTSend >= mqttInterval) {
   lastMQTTSend = millis(); // Zeit aktualisieren
  
    snprintf(buffer, sizeof(buffer),
      "{"
      "\"deviceID\":\"esp32_compost_01\","
      "\"airTemperature\":%.2f,"
      "\"soilTemperature\":%.2f,"
      "\"soilMoisture\":%d,"
      "\"IRDistanceRaw\":%s"
      "}",
      temperature,
      temperatureC,
      percent,
      irDetected? "true" : "false"
    );

  client.publish("iot/compost", buffer);
  Serial.println(buffer);
  }
}
